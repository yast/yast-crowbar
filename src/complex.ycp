/* ------------------------------------------------------------------------------
 * Copyright (c) 2006 Novell, Inc. All Rights Reserved.
 *
 *
 * This program is free software; you can redistribute it and/or modify it under
 * the terms of version 2 of the GNU General Public License as published by the
 * Free Software Foundation.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT
 * ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
 * FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License along with
 * this program; if not, contact Novell, Inc.
 *
 * To contact Novell about this file by physical or electronic mail, you may find
 * current contact information at www.novell.com.
 * ------------------------------------------------------------------------------
 */

/**
 * File:	include/crowbar/complex.ycp
 * Package:	Configuration of crowbar
 * Summary:	Dialogs definitions
 * Authors:     Jiri Suchomel <jsuchome@suse.cz>,
 *              Michal Filka <mfilka@suse.cz>
 *
 * $Id: complex.ycp 65771 2011-09-19 07:37:30Z visnov $
 */

{

textdomain "crowbar";

import "Confirm";
import "Crowbar";
import "CWM";
import "CWMTab";
import "FileUtils";
import "Hostname";
import "Label";
import "Popup";
import "Stage";
import "Wizard";
import "IP";
import "Netmask";

include "crowbar/helps.ycp";

boolean ReallyAbort() {
    return !Crowbar::Modified() || Popup::ReallyAbort(true);
}

/**
 * Read settings dialog
 * @return `abort if aborted and `next otherwise
 */
symbol ReadDialog() {
    Wizard::RestoreHelp(HELPS["read"]:"");
    if (!Confirm::MustBeRoot()) return `abort;
    boolean ret = Crowbar::Read();
    return ret ? `next : `abort;
}

/**
 * Write settings dialog
 * @return `abort if aborted and `next otherwise
 */
symbol WriteDialog() {
    Wizard::RestoreHelp(HELPS["write"]:"");
    boolean ret = Crowbar::Write();
    return ret ? `next : `abort;
}


  // local copy of network settings
  map<string,map> networks    = $[];

  // local copy of user settings
  map<string,map> users       = $[];

  // local copy of teaming options
  map<string,integer> teaming      = $[];

  // local copy of mode
  string mode      = "single";

  string current_network      = "admin";

  // functions for handling password widget
  define void InitPassword (string id) {
    UI::ChangeWidget (`id (id), `Value, users["crowbar","password"]:"");
    UI::ChangeWidget (`id (id), `Enabled, !Crowbar::installed);
  }

  define void StorePassword (string key, map event) {
    users["crowbar","password"] = UI::QueryWidget (`id(key), `Value);
  }

  define symbol HandlePassword (string key, map event) {
    if (event["ID"]:nil == `next) StorePassword (key, event);
    return nil;
  }

  // functions for handling network mode widget
  define void InitMode (string id) {
    list items	=  [
	`item (`id ("single"), "single", "single" == mode),
	`item (`id ("dual"), "dual", "dual" == mode),
	`item (`id ("team"), "team", "team" == mode),
    ];
    UI::ChangeWidget (`id (id), `Items, items);
    UI::ChangeWidget (`id (id), `Enabled, !Crowbar::installed);
  }

  define void StoreMode (string key, map event) {
    mode        = (string) UI::QueryWidget (`id(key), `Value);
  }

  define symbol HandleMode (string key, map event) {
    StoreMode (key, event);
    UI::ChangeWidget (`id ("teaming"), `Enabled, mode == "team");
    return nil;
  }

  // functions for handling network teaming widget
  define void InitTeaming (string id) {
    integer value       = teaming["mode"]:0;
    list items          = [];
    integer i           = 1;
    while (i < 7)
    {
      items     = add (items, `item (`id (i), tostring (i), i == value));
      i         = i + 1;
    }
    UI::ChangeWidget (`id (id), `Items, items);
    UI::ChangeWidget (`id (id), `Enabled, mode == "team");
    UI::ChangeWidget (`id (id), `Enabled, !Crowbar::installed);
  }

  define void StoreTeaming (string key, map event) {
    teaming["mode"]     = (integer) UI::QueryWidget (`id(key), `Value);
  }

  define symbol HandleTeaming (string key, map event) {
    if (event["ID"]:nil == `next) StoreTeaming (key, event);
    return nil;
  }


  /**
   * Validation function for widgets with time values
   */
  define boolean ValidatePassword (string key, map event) {
    if (UI::QueryWidget (`id("password"), `Value) !=
        UI::QueryWidget (`id("repeat_password"), `Value))
    {
      // error popup
      Popup::Error(_("The passwords do not match.
Try again."));
      UI::SetFocus (`id(key));
      return false;
    }
    return true;
  }

  /*
   * Returns current subnet as filled in dialog
   */
  define string GetSubnet()
  {
        return (string) UI::QueryWidget( `id( "subnet"), `Value);
  }

  /*
   * Returns current netmask as filled in dialog
   */
  define string GetNetmask()
  {
        return (string) UI::QueryWidget( `id( "netmask"), `Value);
  }

  /*
   * Returns broadcast address. Based on current netmask and subnet
   */
  define string GetBroadcast()
  {
      return IP::ComputeBroadcast( GetSubnet(), GetNetmask());
  }

  define term CreateItem( string name)
  {
      return `item (`id (name),
        name,
        networks[name,"subnet"]:"",
        networks[name,"netmask"]:"",
        // table entry (VLAN status)
        networks[name,"use_vlan"]:false ? sformat ("%1", networks[name,"vlan"]:0) : _("disabled")
      );
  }

  define list<term> CreateItemList()
  {
      return maplist (string name, map n, networks, {
          return CreateItem( name);
      });
  }
  /**
   * universal widget: initialize the string value of widget @param
   */
  define void InitNetwork (string id) {
    UI::ChangeWidget (`id (id), `Enabled, !Crowbar::installed);
    if( id == "broadcast")
    {
        UI::ChangeWidget (`id (id), `Value, GetBroadcast());
        return;
    }

    UI::ChangeWidget (`id (id), `Value, networks[current_network,id]:"");
  }

  /**
   * universal widget: initialize the integer value of widget @param
   */
  define void InitInteger (string id) {

    UI::ChangeWidget (`id (id), `Value, networks[current_network,id]:0);
    if (id == "vlan")
    {
        UI::ChangeWidget (`id (id), `Enabled, networks[current_network,"use_vlan"]:false == true);
    }
    UI::ChangeWidget (`id (id), `Enabled, !Crowbar::installed);
  }

  /**
   * initialize the value of table
   */
  define void InitNetworkSelect (string id) {
    UI::ChangeWidget (`id ( id), `Items, CreateItemList());
    UI::ChangeWidget (`id(id), `CurrentItem, current_network);
    UI::ChangeWidget (`id (id), `Enabled, !Crowbar::installed);
  }

  /**
   * store the string value of given widget
   */
  define void StoreNetwork (string key, map event) {
    if (key == "router" && UI::QueryWidget (`id(key), `Value) == "")
    {
        if (haskey (networks[current_network]:$[], key))
        {
          // do not save empty router values to json
          networks[current_network]       = remove (networks[current_network]:$[], key);
        }
        return;
    }
    if( key == "subnet")
    {
        networks[ current_network, key] = IP::ComputeNetwork( GetSubnet(), GetNetmask());
        return;
    }
    networks[current_network,key]      = UI::QueryWidget (`id(key), `Value);
  }

  /**
   * Validate entered network values
   */
  define boolean ValidateNetwork( string key, map event)
  {
      boolean ret = true;
      string subnet = GetSubnet();
      string netmask = GetNetmask();
      string router = (string) UI::QueryWidget (`id ("router"), `Value);

      ret = (key != "netmask") || Netmask::Check(netmask);
      if (!ret)
      {
          // error popup
          Popup::Error (sformat (_("The netmask '%1' is invalid.\n%2"), netmask, IP::Valid4 ()));
          return false;
      }

      ret = (key != "subnet") || IP::Check (subnet);
      if (!ret)
      {
          // error popup
          Popup::Error (sformat (_("The IP address '%1' is invalid.\n%2"), subnet, IP::Valid4 ()));
          return false;
      }
      if (key == "router" && router != "")
      {
          if (!IP::Check (router))
          {
            // error popup
            Popup::Error (sformat (_("The router address '%1' is invalid.\n%2"), router, IP::Valid4 ()));
            return false;
          }
          if (IP::ComputeNetwork (subnet,netmask) != IP::ComputeNetwork (router, netmask))
          {
            // error popup
            Popup::Error (sformat (_("The router address '%1' is not part of network '%2'."), router, current_network));
            return false;
          }
      }
      // check if ranges are still in network
      if (key == "subnet")
      {
          boolean ranges_fine   = true;
          foreach (string name, map range, networks[current_network,"ranges"]:$[], {
            foreach (string part, [ "start", "end" ], {
              string ip   = range[part]:"";
              if (IP::ComputeNetwork (ip, netmask) != subnet)
              {
                ranges_fine    = false;
              }
            });
          });
          if (!ranges_fine)
          {
            // popup message
            Popup::Warning (sformat (_("Some range addresses are is not part of network '%1'.
Adapt them using 'Edit ranges' button."), current_network));
            return false;
          }
      }
      return ret;
  }

  /**
   * handler for general string-value widgets: store their value on exit/save
   */
  define symbol HandleNetwork (string key, map event) {

    if (key == "use_vlan" && event["ID"]:nil == "use_vlan")
    {
        UI::ChangeWidget (`id ("vlan"), `Enabled, UI::QueryWidget (`id(key), `Value) == true);
    }
    // store the value on exiting
    if (event["EventReason"]:"" == "ValueChanged")
    {
        if (IP::Check( GetSubnet()) && IP::Check( GetNetmask()))
        {
            UI::ChangeWidget( `id( "broadcast"), `Value, GetBroadcast());
            StoreNetwork( event[ "ID"]:"", event);

            InitNetworkSelect ("network_select");
        }
    }
    if (event["ID"]:nil == `next) StoreNetwork (key, event);
    return nil;
  }


  /**
   * universal widget: initialize the string value of widget @param
   */
  define void InitCheckBox (string id) {
    UI::ChangeWidget (`id (id), `Value, networks[current_network,id]:false);
    UI::ChangeWidget (`id (id), `Enabled, !Crowbar::installed);
  }

  /**
   * handler network selection table
   */
  define symbol HandleNetworkSelect (string key, map event) {

    string selected = (string) UI::QueryWidget (`id(key), `Value);
    if (selected != current_network)
    {
      boolean validated = true;
      foreach (string key, ["netmask", "subnet", "router"], {
        validated       = validated && ValidateNetwork (key, event);
      });
      if (!validated)
      {
        UI::ChangeWidget (`id(key), `CurrentItem, current_network);
        return nil;
      }
      foreach (string key, ["netmask", "subnet", "add_bridge", "use_vlan", "vlan", "broadcast", "router"], {
        StoreNetwork (key, $[]);
      });
      current_network   = selected;
      foreach (string key, ["netmask", "subnet", "broadcast", "router"], {
        InitNetwork (key);
      });
      InitCheckBox ("use_vlan");
      InitCheckBox ("add_bridge");
      InitInteger ("vlan");
    }
    return nil;
  }

  /**
   * handler for ranges button
   */
  symbol HandleRangesButton (string key, map event) {
    any ID	= event["ID"]:nil;
    if (ID != key)
      return nil;

    string subnet               = networks[current_network,"subnet"]:"";
    string netmask              = networks[current_network,"netmask"]:"";
    map<string,map> ranges      = networks[current_network,"ranges"]:$[];
    term ranges_term    = `VBox ();

    foreach (string name, map range, ranges, {

      term r = `Frame (name, `HBox (
        `InputField (`id (name + "_start"), `opt (`hstretch),
          // inputfield label
          _("Min IP Address"), range["start"]:""),
        `InputField (`id (name + "_end"), `opt (`hstretch),
          // inputfield label
          _("Max IP Address"), range["end"]:"")
      ));
      ranges_term       = add (ranges_term, r);
    });

    UI::OpenDialog (`opt (`decorated), `HBox (`HSpacing (1), `VBox(
      `VSpacing (0.5),
      `HSpacing (65),
      ranges_term,
      `VSpacing (0.5),
      `ButtonBox (
        `PushButton (`id(`ok), Label::OKButton()),
        `PushButton (`id(`cancel), Label::CancelButton())
      ),
      `VSpacing (0.5)), `HSpacing (1))
    );

    foreach (string name, map range, ranges, {
      UI::ChangeWidget (`id (name + "_start"),`ValidChars, IP::ValidChars4 + IP::ValidChars6);
      UI::ChangeWidget (`id (name + "_end"),`ValidChars, IP::ValidChars4 + IP::ValidChars6);
      UI::ChangeWidget (`id (name + "_start"), `Enabled, !Crowbar::installed);
      UI::ChangeWidget (`id (name + "_end"), `Enabled, !Crowbar::installed);
    });

    UI::ChangeWidget (`id (`ok), `Enabled, !Crowbar::installed);

    symbol ret = `not_next;
    while (true)
    {
	ret     = (symbol) UI::UserInput ();
        if (ret == `cancel)
        {
          break;
        }
        if (ret == `ok)
        {
          string widget         = "";
          list<list> ranges_l   = [];
          foreach (string name, map range, ranges, {
            if (widget != "")
            {
              return;
            }
            foreach (string part, [ "start", "end" ], {
              string ip   = (string) UI::QueryWidget (`id (name + "_" + part), `Value);
              if (!IP::Check (ip))
              {
                Popup::Error (IP::Valid4 ());
                widget  = name + "_" + part;
              }
              else if (IP::ComputeNetwork (ip, netmask) != subnet)
              {
                Popup::Error (sformat (_("The address '%1' is not part of network '%2'."),
                  ip, current_network));
                widget  = name + "_" + part;
              }
              ranges[name,part] = ip;
            });
            if (widget == "" &&
                IP::ToInteger (ranges[name,"start"]:"") > IP::ToInteger (ranges[name,"end"]:""))
            {
              // error message
              Popup::Error (_("The lowest address must be lower than the highest one."));
              widget  = name + "_end";
            }
            else
            {
              ranges_l  = add (ranges_l, [
                IP::ToInteger (ranges[name,"start"]:""),
                IP::ToInteger (ranges[name,"end"]:""),
                name
              ]);
            }
          });
          // check if ranges do not overlap
          if (widget == "" && size (ranges_l) > 1)
          {
            ranges_l    = sort (list a, list b, ranges_l, {
              return (a[0]:0 <= b[0]:0);
            });
            integer i   = 0;
            while (i < size (ranges_l) - 1) {
              list this = ranges_l[i]:[];
              list next = ranges_l[i + 1]:[];
              if (this[1]:0 >= next[0]:0)
              {
                // error message
                Popup::Error (sformat (_("Ranges '%1' and '%2' are overlapping."),
                  this[2]:"", next[2]:""));
                widget  = next[2]:"" + "_start";
              }
              i = i + 1;
            }
          }
          // finally, save the ranges
          if (widget == "")
          {
            networks[current_network,"ranges"]  = ranges;
            break;
          }
          else
          {
            ret = `not_next;
            UI::SetFocus (`id (widget));
            continue;
          }
        }
    }

    UI::CloseDialog ();
    return nil;
  }

map<string, map> widget_description	= $[
    "password" : $[
	"widget"	: `password,
	"opt"		: [ `hstretch ],
	// textentry label
	"label"		: _("Password for Crowbar Administrator"),
	// help text
	"help"		: _("<p>Enter the password for Crowbar administrator.</p>"),
	"init"		: InitPassword,
	"store"		: StorePassword,
	"handle"	: HandlePassword,
	"validate_type"	: `function,
	"validate_function"	: ValidatePassword,
    ],
    "repeat_password" : $[
	"widget"	: `password,
	"opt"		: [ `hstretch ],
	// textentry label
	"label"		: _("Repeat the Password"),
	"init"		: InitPassword,
    ],
    "mode" : $[
	"widget"	: `combobox,
	"opt"		: [ `hstretch, `notify ],
	// textentry label
	"label"		: _("Mode"),
        "no_help"       : true,
	"init"		: InitMode,
	"store"		: StoreMode,
	"handle"	: HandleMode,
    ],
    "teaming" : $[
	"widget"	: `combobox,
	// textentry label
	"label"		: _("Bonding Policy"),
        "no_help"       : true,
	"init"		: InitTeaming,
	"store"		: StoreTeaming,
	"handle"	: HandleTeaming,
    ],
    // ---------------- widgets for Network tab

    "network_help"      : $[
        "widget"        : `empty,
        // generic help for Network tab
        "help"          : HELPS["overview"]:"",
    ],
    "network_select" : $[
        "widget"        : `custom,
        "custom_widget" :
          `VBox (
              `VWeight (2, `Table (`id( "network_select"),
                  `opt (`notify, `immediate, `hstretch),
                  // table header
                  `header (_("Network"), _("Subnet Address"), _("Network Mask"), _("VLAN"))
              ))
          ),
        "init"          : InitNetworkSelect,
        "handle"        : HandleNetworkSelect,
    ],
    "use_vlan" : $[
	"widget"	: `checkbox,
	// checkbox label
	"label"		: _("Use VLAN"),
        "no_help"       : true,
	"init"		: InitCheckBox,
	"store"		: StoreNetwork,
	"handle"	: HandleNetwork,
        "opt"           : [ `notify ],
    ],
    "vlan" : $[
	"widget"	: `intfield,
	// textentry label
	"label"		: _("VLAN ID"),
        "no_help"       : true,
	"init"		: InitInteger,
	"store"		: StoreNetwork,
	"handle"	: HandleNetwork,
	"opt"		: [ `notify, `hstretch ],
    ],
    "router" : $[
	"widget"	: `textentry,
	// textentry label
	"label"		: _("Router"),
        "validate_type" : `function,
        "validate_function": ValidateNetwork,
        "valid_chars"   : IP::ValidChars4 + IP::ValidChars6,
        "no_help"       : true,
	"init"		: InitNetwork,
	"store"		: StoreNetwork,
	"handle"	: HandleNetwork,
        "opt"           : [ `notify ],
    ],
    "subnet" : $[
	"widget"	: `textentry,
	// textentry label
	"label"		: _("Subnet"),
        "handle_events" : [ "ValueChanged" ],
        "validate_type" : `function,
        "validate_function": ValidateNetwork,
        "valid_chars"   : IP::ValidChars4 + IP::ValidChars6,
        "no_help"       : true,
	"init"		: InitNetwork,
	"store"		: StoreNetwork,
	"handle"	: HandleNetwork,
        "opt"           : [ `notify ],
    ],
    "netmask" : $[
	"widget"	: `textentry,
	// textentry label
	"label"		: _("Netmask"),
        "handle_events" : [ "ValueChanged" ],
        "validate_type" : `function,
        "validate_function": ValidateNetwork,
        "valid_chars"   : IP::ValidChars4 + IP::ValidChars6,
        "no_help"       : true,
	"init"		: InitNetwork,
	"store"		: StoreNetwork,
	"handle"	: HandleNetwork,
        "opt"           : [ `notify ],
    ],
    "broadcast" : $[
	"widget"	: `textentry,
	"label"		: _("Broadcast"),
        "valid_chars"   : IP::ValidChars4 + IP::ValidChars6,
        "no_help"       : true,
	"init"		: InitNetwork,
	"store"		: StoreNetwork,
	"opt"		: [ `disabled ]
    ],
    "add_bridge" : $[
	"widget"	: `checkbox,
	// checkbox label
	"label"		: _("Add Bridge"),
        "no_help"       : true,
	"init"		: InitCheckBox,
	"store"		: StoreNetwork,
	"handle"	: HandleNetwork,
    ],
    "ranges_button"	: $[
	"widget"	: `push_button,
	// push button label
	"label"		: _("&Edit Ranges..."),
        "no_help"       : true,
	"handle"	: HandleRangesButton
    ],
];

// description of tab layouts
map get_tabs_descr () {
    return $[
    "admin" : $[
	// tab header
	"header"	: _("Administration Settings"),
	"contents"	: `HBox (`HSpacing(2), `VBox (
	    `VSpacing (),
            "password",
            "repeat_password",
	    `VStretch ()
	), `HSpacing(2)),
	"widget_names" : [
            "password", "repeat_password"
	],
    ],
    "network_mode"      : $[
	// tab header
	"header"	: _("Network Mode"),
	"contents"	: `HBox (`HSpacing(2), `VBox (
	    `VSpacing (),
            `HBox (
              "mode",
              "teaming"
            ),
	    `VStretch ()
	), `HSpacing(2)),
	"widget_names" : [
            "mode", "teaming"
	],
    ],
    "networks"	: $[
	// tab header
	"header"	: _("Networks"),
	"contents"	: `HBox (`HSpacing(2), `VBox (
            "network_help",
	    `VSpacing (),
            "network_select",
	    `Left ("add_bridge"),
	    `VSpacing (0.4),
            `HBox (
              `VBox (
                "use_vlan",
                `Label ("")
              ),
              `HSpacing (2),
              "vlan"
            ),
            "router",
            `HBox (
	      "subnet",
	      "netmask",
	      "broadcast"
            ),
	    `VSpacing (0.4),
            `Right ("ranges_button")
	), `HSpacing (2)),
	"widget_names" : [
            "network_select", "vlan", "router", "subnet", "netmask", "broadcast", "add_bridge",
            "ranges_button", "use_vlan", "network_help",
	],
    ],
    ];
}

symbol OverviewDialog() {

    networks    = Crowbar::networks;
    mode        = Crowbar::mode;
    teaming     = Crowbar::teaming;
    users       = Crowbar::users;

    widget_description["tab"] = CWMTab::CreateWidget($[
	"tab_order"	: [ "admin", "networks", "network_mode" ],
	"tabs"		: get_tabs_descr (),
	"widget_descr"	: widget_description,
	"initial_tab"	: "admin",
    ]);

    Wizard::SetContentsButtons("", `VBox (), "",
        Label::BackButton(), Label::FinishButton());

    if (Crowbar::installed)
    {
        // popup message %1 is FQDN
        Popup::Message (sformat (_("The SUSE Cloud Admin Server has been deployed. Changing the network is
currently not supported.

You can visit the Crowbar web UI on http://%1:3000/"), Hostname::CurrentFQ ()));
    }


    symbol ret = CWM::ShowAndRun ($[
	"widget_names"		: [
	    "tab"
	],
	"widget_descr"		: widget_description,
	"contents"		: `VBox ("tab"),
	// default dialog caption
        "caption"               : _("Crowbar Configuration Overview"),
	"abort_button"		: Stage::cont () ? Label::AbortButton () : nil,
	"next_button"		: Stage::cont () ? Label::NextButton () : Label::OKButton (),
	"back_button"		: Stage::cont () ? Label::BackButton () : Label::CancelButton (),
    ]);
    if (Crowbar::installed)
    {
      // not saving
      return `back;
    }
    if (ret == `next)
    {
      Crowbar::networks = networks;
      Crowbar::users    = users;
      Crowbar::teaming  = teaming;
      Crowbar::mode     = mode;
    }
    return ret;
}

/* EOF */
}
